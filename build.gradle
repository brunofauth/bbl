buildscript {
    ext {
        kotlin_version = '1.9.21'
        lucene_version = '9.4.0'
        tinlylog_version = '2.5.0'
        junit_version = '5.9.1'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'application'
    id "org.beryx.jlink" version "3.0.1"
    id "org.javamodularity.moduleplugin" version "1.8.12"
}

group = 'org.gnit.bible.cli'
version = '1.4'
def appName = 'bbl'

task versionTxt() {
    doLast {
        new File(projectDir, "src/main/resources/version.txt").text = """
            bbl version $version
            While you are in front of your console, you are not alone. God is with you.
            Always go back to the Word of God especially in difficulty.
""".stripIndent()
    }
}

repositories {
    mavenCentral()
    maven { url "file:../bbl-core/.m2/repository" }
}

dependencies {
    implementation "org.gnit.bible:bbl-core:1.0-SNAPSHOT@jar"
    implementation "com.github.ajalt.clikt:clikt:3.5.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
    implementation 'org.slf4j:slf4j-api:2.0.3'
    implementation "org.tinylog:slf4j-tinylog:$tinlylog_version"
    implementation "org.tinylog:tinylog-impl:$tinlylog_version"
    implementation 'net.java.dev.jna:jna:5.12.1'

    //search
    implementation 'com.google.jimfs:jimfs:1.2'
    implementation "org.apache.lucene:lucene-queryparser:$lucene_version"
    implementation "org.apache.lucene:lucene-analysis-common:$lucene_version"
    implementation "org.apache.lucene:lucene-analysis-smartcn:$lucene_version"
    implementation "org.apache.lucene:lucene-analysis-kuromoji:$lucene_version"
    implementation "org.apache.lucene:lucene-analysis-nori:$lucene_version"
    implementation "org.apache.lucene:lucene-analysis-morfologik:$lucene_version"

    //test
    implementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
}

//https://stackoverflow.com/questions/25873971/docker-cache-gradle-dependencies
task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                configuration.resolve()
            }
            subProject.configurations.each { configuration ->
                if(configuration.canBeResolved == true){
                    configuration.resolve()
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '21'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '21'
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources.dependsOn(versionTxt)

compileKotlin.destinationDirectory = compileJava.destinationDirectory

application {
    mainClassName = 'org.gnit.bible.cli.MainKt'
    mainModule = 'org.gnit.bible.cli'
}

jar {
     manifest {
         attributes 'Main-Class': 'org.gnit.bible.cli.MainKt'
     }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from('src/main/resources') {
        include 'data/**/*.txt'
    }

    exclude 'tinylog-dev.properties'
}

jlink {

    mergedModule {
        additive = true
        forceMerge "kotlin"
    }

    options = [
        '--strip-debug',
        '--compress', 'zip-9',
        '--no-header-files',
        '--no-man-pages'
    ]

    def installerResources = "src/main/resources/installer"

    // Generate script files for launching the app. Env vars be included like this: {{ MY_VAR }}
    // {{BIN_DIR}} - the bin directory of the custom runtime image
    // {{HOME_DIR}} - user’s home directory ($HOME on Unix-like systems, %USERPROFILE% on Windows)
    launcher {
        name = "$appName"
        // jvmArgs = ''
        // args = ''
        // noConsole = ''
        // unixScriptTemplate = ''
        // windowsScriptTemplate = ''
    }

    //https://badass-jlink-plugin.beryx.org/releases/latest/#_jpackage
    jpackage {

        skipInstaller = (System.getenv("SKIP_INSTALLER") ?: "").length() > 0

        // // Convenience property for setting imageOutputDir and installerOutputDir
        // // defaultValue: "jpackage"
        // outputDir = ""

        // // passed to jpackage's '--output' option when executing the 'jpackageImage' task
        // // defaultValue: buildDir/outputDir
        // imageOutputDir = ""

        // // passed to jpackage's '--output' option when executing the 'jpackage' task
        // // defaultValue: buildDir/outputDir
        // installerOutputDir = ""

        // passed to jpackageImage's '--name' option. Defaults to launcher.name or project.name
        // imageName = ""

        // passed to jpackage's '--name' option. Defaults to launcher.name or project.name
        // installerName = ""

        appVersion = "$version"

        installerOptions += [
            '--copyright', 'Copyright © 2022, Joel H. Ide, gnit.org',
            '--vendor', 'Gospel and Information Technology',
            '--description', 'bbl, command line bible reader',
            '--strip-debug'
        ]

        //https://wix-tutorial-ja.github.io/ch01/index.html
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            outputDir = "jpackage/windows"

            icon = "$installerResources/bbl.ico"
            installerOptions += [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut',
                '--resource-dir', (installerResources + "/windows")
            ]

            imageOptions += ['--win-console']

            installerType = 'msi'

            targetPlatform("windows-x64") {
                //jdkHome = "C:\\Program Files\\OpenJDK\\jdk-16"
                jdkHome = "$System.env.JAVA_HOME"
            }
        }

        //https://docs.oracle.com/javase/jp/14/docs/specs/man/jpackage.html
        //https://developer.apple.com/jp/developer-id/
        //https://stackoverflow.com/questions/44081611/how-can-i-change-the-icon-used-by-javapackager-for-pkg-installers
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            outputDir = "jpackage/macos"

            icon = "$installerResources/bbl.icns"
            installerType = 'pkg'
            installerOptions += [/*'--mac-sign', //TODO input after enrolling developer program
                                 '--mac-signing-key-user-name', 'x',*/
                                 '--resource-dir', (installerResources + "/macos")]
        }

        //https://docs.oracle.com/en/java/javase/16/jpackage/override-jpackage-resources.html#GUID-405708DC-0243-49FC-84D9-B2A7F0A011A9
        //https://docs.oracle.com/javase/jp/14/jpackage/override-jpackage-resources.html#GUID-1B718F8B-B68D-4D46-B1DB-003D7729AAB6
        //https://www.debian.org/doc/manuals/debian-faq/pkg-basics.ja.html#maintscripts
        //https://www.ep.sci.hokudai.ac.jp/~epnetfan/zagaku/2000/1006/deb-make.html
        //https://github.com/ewxrjk/greed/tree/master/debian
        //https://www.debian.org/doc/debian-policy/ch-controlfields.html
        //https://www.debian.org/doc/debian-policy/ch-maintainerscripts.html
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            outputDir = "jpackage/linux"

            icon = "$installerResources/bbl.png"
            installerOptions += ['--install-dir', '/usr/local/lib',
                                 '--resource-dir', (installerResources + "/linux"),
                                 '--linux-deb-maintainer', 'nehemiaharchive@gmail.com'
            ]

            installerType = 'deb'

            targetPlatform("linux-x64") {
                //in wsl2 
                //jdkHome = "/usr/lib/jvm/java-17-openjdk-amd64"

                //in gradle:7.5.1-jdk17-focal docker image
                jdkHome = System.getenv("JAVA_HOME") ?: "/opt/java/openjdk"
            }
        }
    }

    tasks.register('cookbook') {
        doLast {
            println "deleting deb package file in cookbook dir."
            delete fileTree('bbl_install/files') {
                include '*.deb'
            }

            println "copying new deb to cookbook dir"
            copy {
                from layout.buildDirectory.dir("jpackage/linux")
                include "*.deb"
                into "bbl_install/files"
            }
            println "integration test ready for linux. move to cookbook dir and run: bundle exec kitchen test"
        }
    }
}
