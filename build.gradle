buildscript {
    ext.ktor_version = "2.0.0-beta-1"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id "org.beryx.jlink" version "2.24.4"
    id "org.javamodularity.moduleplugin" version "1.8.10"
}

group = 'org.gnit.bible'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation("com.github.ajalt.clikt:clikt:3.4.0")
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileKotlin.destinationDirectory = compileJava.destinationDirectory

application {
    mainClassName = 'org.gnit.bible.MainKt'
    mainModule = 'org.gnit.bible'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jlink {
    mergedModule {
        additive = true
        forceMerge "kotlin"
    }

    launcher {
        name = 'bbl'
    }

    jpackage {
        installerName = 'bbl'
        installerOptions += [
                '--app-version', "$version",
                '--copyright', 'Joel H. Ide',
                '--description', 'bbl, bible command line, read bible in your terminal.'
        ]


        OperatingSystem os = org.gradle.internal.os.OperatingSystem.current()
        if (os.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']

            installerType = 'msi'

            targetPlatform("windows-x64") {
                jdkHome = "C:\\Program Files\\OpenJDK\\jdk-16"
            }
        }

        //https://docs.oracle.com/javase/jp/14/docs/specs/man/jpackage.html
        //https://developer.apple.com/jp/developer-id/
        if (os.macOsX) {
            installerType = 'pkg'
        }

        //https://docs.oracle.com/en/java/javase/16/jpackage/override-jpackage-resources.html#GUID-405708DC-0243-49FC-84D9-B2A7F0A011A9
        //https://docs.oracle.com/javase/jp/14/jpackage/override-jpackage-resources.html#GUID-1B718F8B-B68D-4D46-B1DB-003D7729AAB6
        //https://www.debian.org/doc/manuals/debian-faq/pkg-basics.ja.html#maintscripts
        //https://www.debian.org/doc/debian-policy/ch-maintainerscripts.html
        if (os.linux) {

            installerOptions += ['--install-dir', '/opt',
                                 '--linux-deb-maintainer', 'nehemiaharchive@gmail.com']

            installerType = 'deb'

            targetPlatform("linux-x64") {
                jdkHome = "/usr/lib/jvm/java-16-openjdk-amd64"
            }
        }

    }
}
