buildscript {
    ext.ktor_version = "2.0.0-beta-1"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id "org.beryx.jlink" version "2.24.4"
    id "org.javamodularity.moduleplugin" version "1.8.10"
}

group = 'org.gnit.bible'
version = '1.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation 'com.github.ajalt.clikt:clikt:3.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.3.2'
    implementation 'org.jetbrains.kotlin:kotlin-test'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.tinylog:slf4j-tinylog:2.4.1'
    implementation 'org.tinylog:tinylog-impl:2.4.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '16'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '16'
}

compileKotlin.destinationDirectory = compileJava.destinationDirectory

application {
    mainClassName = 'org.gnit.bible.MainKt'
    mainModule = 'org.gnit.bible'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jlink {
    mergedModule {
        additive = true
        forceMerge "kotlin"
    }

    def resourceDir = "src/main/resources/installer"

    launcher {
        name = 'bbl'
    }

    jpackage {
        installerName = 'bbl'

        appVersion = "$version"
        installerOptions += [
                '--copyright', 'Copyright Â© 2022, Joel H. Ide, gnit.org',
                '--vendor', 'Gospel and Information Technology',
                '--description', 'bbl, command line bible reader'
        ]

        //https://wix-tutorial-ja.github.io/ch01/index.html
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            icon = "$resourceDir/bbl.ico"
            installerOptions += ['--win-per-user-install',
                                 '--win-dir-chooser',
                                 '--win-menu',
                                 '--win-shortcut',
                                 '--resource-dir', (resourceDir + "/windows")]

            imageOptions += ['--win-console']

            installerType = 'msi'

            targetPlatform("windows-x64") {
                jdkHome = "C:\\Program Files\\OpenJDK\\jdk-16"
            }
        }

        //https://docs.oracle.com/javase/jp/14/docs/specs/man/jpackage.html
        //https://developer.apple.com/jp/developer-id/
        //https://stackoverflow.com/questions/44081611/how-can-i-change-the-icon-used-by-javapackager-for-pkg-installers
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            icon = "$resourceDir/bbl.icns"
            installerType = 'pkg'
            installerOptions += [/*'--mac-sign', //TODO input after enrolling developer program
                                 '--mac-signing-key-user-name', 'x',*/
                                 '--resource-dir', (resourceDir + "/macos")]
        }

        //https://docs.oracle.com/en/java/javase/16/jpackage/override-jpackage-resources.html#GUID-405708DC-0243-49FC-84D9-B2A7F0A011A9
        //https://docs.oracle.com/javase/jp/14/jpackage/override-jpackage-resources.html#GUID-1B718F8B-B68D-4D46-B1DB-003D7729AAB6
        //https://www.debian.org/doc/manuals/debian-faq/pkg-basics.ja.html#maintscripts
        //https://www.debian.org/doc/debian-policy/ch-maintainerscripts.html
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            icon = "$resourceDir/bbl.png"
            installerOptions += ['--install-dir', '/usr/local/lib',
                                 '--resource-dir', (resourceDir + "/linux"),
                                 '--linux-deb-maintainer', 'nehemiaharchive@gmail.com']

            installerType = 'deb'

            targetPlatform("linux-x64") {
                jdkHome = "/usr/lib/jvm/java-16-openjdk-amd64"
            }
        }

    }
}
