buildscript {
    ext.ktor_version = "2.0.0-beta-1"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id "org.beryx.jlink" version "2.24.4"
    id "org.javamodularity.moduleplugin" version "1.8.10"
}

group = 'org.gnit.bible'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation("com.github.ajalt.clikt:clikt:3.4.0")
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileKotlin.destinationDirectory = compileJava.destinationDirectory

application {
    mainClassName = 'org.gnit.bible.MainKt'
    mainModule = 'org.gnit.bible'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jlink {
    mergedModule {
        additive = true
        forceMerge "kotlin"
    }

    launcher {
        name = 'bbl'
    }

    jpackage {
        installerName = 'bbl'
        installerOptions += [
                '--app-version', "$version",
                '--copyright', 'Joel H. Ide',
                '--description', 'bbl, bible command line, read bible in your terminal.'
        ]

        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']

            installerType = 'msi'

            targetPlatform("windows-x64") {
                jdkHome = "C:\\Program Files\\OpenJDK\\jdk-16"
            }
        }

        //https://docs.oracle.com/javase/jp/14/docs/specs/man/jpackage.html
        //https://developer.apple.com/jp/developer-id/
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            installerType = 'pkg'
        }

        if (org.gradle.internal.os.OperatingSystem.current().linux) {

            installerOptions += ['--install-dir', '/usr/local/bin',
                                 '--linux-deb-maintainer', 'nehemiaharchive@gmail.com']

            targetPlatform("linux-x64") {
                jdkHome = "/usr/lib/jvm/java-16-openjdk-amd64"
            }
        }

    }
}
